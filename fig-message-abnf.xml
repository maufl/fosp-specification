<figure anchor="fosp-abnf">
	<preamble />
	<artwork type="abnf"><![CDATA[
message       = request / response / notification

request       = request-type SP ( resource-id / "*" )
                SP sequence-number CRLF headers [ CRLF body ]

request-type  = "OPTIONS" / "AUTH" / "CREATE" / "GET" / "LIST"
                / "PATCH" / "DELETE" / "READ" / "WRITE"

response      = response-type SP response-status
                SP sequence-number CRLF headers [ CRLF body ]

response-type = "SUCCEDED" / "FAILED"

notification  = event-type SP resource-id CRLF headers [ CRLF body ]

event-type    = "CREATED" / "UPDATED" / "DELETED"

resource-id   = iunreserved "@" ihost ipath-absolute

headers       = *( header CRLF )

header        = header-key ":" header-value

header-key    = ALPHA *( ALPHANUM / "-" ) ALPHANUM

header-value  = 1*VCHAR

ALPHANUM      = ALPHA / DIGIT

body          = *OCTET
		]]></artwork>
	<postamble />
</figure>
